plugins {
    id 'org.springframework.boot' version '3.2.4' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    // Apply application-specific plugins only to service projects, not to libraries like 'model'
    if (project.name != 'model') {
        apply plugin: 'application'
        apply plugin: 'org.springframework.boot'
        
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }

    }

    repositories {
        mavenCentral()
    }

    version = findProperty('serviceVersion') ?: 'NO.VERSION.SET'

    task printVersion {
        doLast {
            println project.version
        }
    }
}
